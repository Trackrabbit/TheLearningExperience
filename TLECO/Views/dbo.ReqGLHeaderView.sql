SET QUOTED_IDENTIFIER OFF
GO
SET ANSI_NULLS ON
GO
-- GL Header View Joining GL10000, GL20000 and GL30000 
CREATE VIEW [dbo].[ReqGLHeaderView] AS 
select  
	1 "TrxState", -- Set to 1 for Work 
	BACHNUMB, 
	BCHSOURC,	 
	(SELECT CREATDDT FROM SY00500 WHERE GL10000.BACHNUMB = SY00500.BACHNUMB AND GL10000.BCHSOURC = SY00500.BCHSOURC) AS BatchCreatedDate, 
	(SELECT TIME1 FROM SY00500 WHERE GL10000.BACHNUMB = SY00500.BACHNUMB AND GL10000.BCHSOURC = SY00500.BCHSOURC) AS BatchCreatedTime, 
	JRNENTRY, 
	convert(bigint, RCTRXSEQ) "RCTRXSEQ", 
	SOURCDOC, 
	REFRENCE, 
	TRXDATE, 
	LASTUSER, 
	LSTDTEDT, 
	DEX_ROW_TS, 
	USWHPSTD, 
	TRXSORCE, 
	SERIES, 
	ORPSTDDT, 
	ORTRXSRC, 
	CURNCYID, 
	(select ISOCURRC from DYNAMICS..MC40200 where CURNCYID = GL10000.CURNCYID) "ISOCURRC", 
	(select CMPANYID from DYNAMICS..SY01500 where GL10000.ORCOMID = INTERID) "CMPANYID", 
	ORIGINJE, 
	VOIDED, 
	Ledger_ID, 
	0 "YEAR", -- This is a place holder so we can join on JRNENTRY and YEAR for Unioned tables 
	DEX_ROW_ID 
from 
	GL10000 
	WHERE RCTRXSEQ = 0 
 
union all 
 
select distinct  
	2 "TrxState", -- Set to 2 for Open 
	'' "BACHNUMB", -- BACHNUMB does not exist in OPEN record 
	'' "BCHSOURC", -- BCHSOURC does not exist in OPEN record 
	'', --BatchCreatedDate 
	'', --BatchCreatedTime 
	JRNENTRY, 
	convert(bigint, RCTRXSEQ) "RCTRXSEQ", 
	SOURCDOC, 
	REFRENCE, 
	TRXDATE, 
	LASTUSER, 
	LSTDTEDT, 
	MAX(DEX_ROW_TS) "DEX_ROW_TS", 
	USWHPSTD, 
	TRXSORCE, 
	SERIES, 
	ORPSTDDT, 
	ORTRXSRC, 
	CURNCYID, 
	(select ISOCURRC from DYNAMICS..MC40200 where CURNCYID = GL20000.CURNCYID) "ISOCURRC", 
	(select CMPANYID from DYNAMICS..SY01500 where GL20000.ORCOMID = INTERID) "CMPANYID", 
	ORIGINJE, 
	VOIDED, 
	Ledger_ID, 
	OPENYEAR, 
	Max(DEX_ROW_ID) as DEX_ROW_ID 
from  
	GL20000 
group by JRNENTRY, RCTRXSEQ, SOURCDOC, REFRENCE, TRXDATE, LASTUSER, LSTDTEDT, USWHPSTD, TRXSORCE, SERIES, ORPSTDDT, ORTRXSRC, CURNCYID, ORIGINJE, VOIDED, OPENYEAR, ORCOMID, Ledger_ID 
 
union all 
 
select distinct 
	3 "TrxState", -- Set to 3 for History 
	'' "BACHNUMB", -- BACHNUMB does not exist in HISTORY record 
	'' "BCHSOURC", -- BCHSOURC does not exist in HISTORY record 
	'', --BatchCreatedDate 
	'', --BatchCreateTime 
	JRNENTRY, 
	convert(bigint, RCTRXSEQ) "RCTRXSEQ", 
	SOURCDOC, 
	REFRENCE, 
	TRXDATE, 
	LASTUSER, 
	LSTDTEDT, 
	MAX(DEX_ROW_TS) "DEX_ROW_TS", 
	USWHPSTD, 
	TRXSORCE, 
	SERIES, 
	ORPSTDDT, 
	ORTRXSRC, 
	CURNCYID, 
	(select ISOCURRC from DYNAMICS..MC40200 where CURNCYID = GL30000.CURNCYID) "ISOCURRC", 
	(select CMPANYID from DYNAMICS..SY01500 where GL30000.ORCOMID = INTERID) "CMPANYID", 
	ORIGINJE, 
	VOIDED, 
	Ledger_ID, 
	HSTYEAR,  
	Max(DEX_ROW_ID) as DEX_ROW_ID 
from  
	GL30000 
group by JRNENTRY, RCTRXSEQ, SOURCDOC, REFRENCE, TRXDATE, LASTUSER, LSTDTEDT, USWHPSTD, TRXSORCE, SERIES, ORPSTDDT, ORTRXSRC, CURNCYID, ORIGINJE, VOIDED, HSTYEAR, ORCOMID, Ledger_ID 
GO
GRANT SELECT ON  [dbo].[ReqGLHeaderView] TO [DYNGRP]
GO
GRANT INSERT ON  [dbo].[ReqGLHeaderView] TO [DYNGRP]
GO
GRANT DELETE ON  [dbo].[ReqGLHeaderView] TO [DYNGRP]
GO
GRANT UPDATE ON  [dbo].[ReqGLHeaderView] TO [DYNGRP]
GO
